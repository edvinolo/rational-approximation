#:include "../include/common.fypp"
module rational_interpolation
    use kinds
    use stdlib_math, only: is_close
    use ieee_arithmetic, only: ieee_is_finite
    use iso_fortran_env, only: stdout => output_unit, stderr => error_unit
    implicit none

    #:for d,typ in list(zip(DOMAIN_PREFIX,WP_TYPES))
    type, abstract, public :: interp_base_${d}$(wp)
        integer, kind :: wp
        integer :: N
        ${typ}$, allocatable :: x(:)
        ${typ}$, allocatable :: y(:)
    contains
        generic :: init => #{for KIND in KINDS[:-1]}#init_${d}$_${KIND}$, #{endfor}#init_${d}$_${KINDS[-1]}$
        generic :: eval => #{for k in KINDS[:-1]}#eval_vec_${d}$_${k}$, eval_scalar_${d}$_${k}$, #{endfor}#eval_vec_${d}$_${KINDS[-1]}$, eval_scalar_${d}$_${KINDS[-1]}$

        #:for KIND in KINDS
        procedure(${d}$_init_${KIND}$), deferred :: init_${d}$_${KIND}$
        procedure(${d}$_eval_scalar_${KIND}$), private, deferred :: eval_scalar_${d}$_${KIND}$
        procedure(${d}$_eval_vec_${KIND}$), private, deferred :: eval_vec_${d}$_${KIND}$
        #:endfor
    end type interp_base_${d}$

    #:endfor

    #:for d,typ in list(zip(DOMAIN_PREFIX,WP_TYPES))
    type, extends(interp_base_${d}$), public :: thiele_interp_${d}$
        ${typ}$, allocatable :: rho(:)
    contains
        #:for k in KINDS
        procedure :: init_${d}$_${k}$ => thiele_init_${d}$_${k}$
        procedure :: eval_scalar_${d}$_${k}$ => thiele_eval_scalar_${d}$_${k}$
        procedure :: eval_vec_${d}$_${k}$ => thiele_eval_vec_${d}$_${k}$
        #:endfor
    end type thiele_interp_${d}$

    #:endfor

    #:for d,typ in list(zip(DOMAIN_PREFIX,WP_TYPES))
    type, extends(interp_base_${d}$), public :: MTT_interp_${d}$
        ${typ}$, allocatable :: a(:)
        ${typ}$, allocatable :: x_p(:)
        ${typ}$, allocatable :: y_p(:)
        logical :: is_constant
        integer :: t
    contains
        #:for k in KINDS
        procedure :: init_${d}$_${k}$ => MTT_init_${d}$_${k}$
        procedure :: eval_scalar_${d}$_${k}$ => MTT_eval_scalar_${d}$_${k}$
        procedure :: eval_vec_${d}$_${k}$ => MTT_eval_vec_${d}$_${k}$
        #:endfor
    end type MTT_interp_${d}$

    #:endfor

    abstract interface
    #:for d,typ in DOMAINS
    #:for KIND in KINDS
        subroutine ${d}$_init_${KIND}$(this,x,y)
            import :: ${KIND}$
            import :: interp_base_${d}$
            class(interp_base_${d}$(${KIND}$)), intent(out) :: this
            ${typ}$(${KIND}$), intent(in) :: x(:)
            ${typ}$(${KIND}$), intent(in) :: y(:)
        end subroutine ${d}$_init_${KIND}$

        function ${d}$_eval_vec_${KIND}$(this,x) result(res)
            import :: ${KIND}$
            import :: interp_base_${d}$
            class(interp_base_${d}$(${KIND}$)), intent(in) :: this
            ${typ}$(${KIND}$), intent(in) :: x(:)
            ${typ}$(${KIND}$), allocatable :: res(:)
        end function ${d}$_eval_vec_${KIND}$

        function ${d}$_eval_scalar_${KIND}$(this,x) result(res)
            import :: ${KIND}$
            import :: interp_base_${d}$
            class(interp_base_${d}$(${KIND}$)), intent(in) :: this
            ${typ}$(${KIND}$), intent(in) :: x
            ${typ}$(${KIND}$) :: res
        end function ${d}$_eval_scalar_${KIND}$

    #:endfor
    #:endfor
    end interface

    interface is_finite
    #:for d in DOMAIN_PREFIX
    #:for k in KINDS
        module procedure :: is_finite_${d}$_${k}$
    #:endfor
    #:endfor
    end interface

contains
    #:for d,typ in DOMAINS
    #:for k in KINDS
    subroutine thiele_init_${d}$_${k}$(this,x,y)
        class(thiele_interp_${d}$(${k}$)), intent(out) :: this
        ${typ}$(${k}$), intent(in) :: x(:)
        ${typ}$(${k}$), intent(in) :: y(:)

        ${typ}$(${k}$), allocatable :: rho_temp(:,:)
        integer :: i

        this%x = x
        this%y = y
        this%N = size(x)

        allocate(rho_temp(this%N,this%N))
        rho_temp = 0
        rho_temp(:,1) = this%y
        rho_temp(:this%N-1,2) = (this%x(:this%N-1) - this%x(2:))/(rho_temp(:this%N-1,1) - rho_temp(2:,1))

        do i = 3,this%N
            rho_temp(:this%N-i+1,i) = (this%x(:this%N-i+1) - this%x(i:))&
                                        /(rho_temp(:this%N-i+1,i-1) - rho_temp(2:this%N-i+2,i-1)) &
                                        + rho_temp(2:this%N-i+2,i-2)
        end do

        this%rho = rho_temp(1,:)
    end subroutine thiele_init_${d}$_${k}$

    function thiele_eval_scalar_${d}$_${k}$(this,x) result(res)
        class(thiele_interp_${d}$(${k}$)), intent(in) :: this
        ${typ}$(${k}$), intent(in) :: x
        ${typ}$(${k}$) :: res

        integer :: i
        res = 0

        do i = this%N,3,-1
            res = (x - this%x(i-1))/(this%rho(i) - this%rho(i-2) + res)
        end do

        res = this%rho(1) + (x - this%x(1))/(this%rho(2) + res)

    end function thiele_eval_scalar_${d}$_${k}$

    function thiele_eval_vec_${d}$_${k}$(this,x) result(res)
        class(thiele_interp_${d}$(${k}$)), intent(in) :: this
        ${typ}$(${k}$), intent(in) :: x(:)
        ${typ}$(${k}$), allocatable :: res(:)

        integer :: i

        allocate(res(size(x)))
        res = 0

        do i = 1,size(x)
            res(i) = this%eval(x(i))
        end do

    end function thiele_eval_vec_${d}$_${k}$

    #:endfor
    #:endfor

    #:for d,typ in DOMAINS
    #:for k in KINDS
    subroutine MTT_init_${d}$_${k}$(this,x,y)
        class(MTT_interp_${d}$(${k}$)), intent(out) :: this
        ${typ}$(${k}$), intent(in) :: x(:)
        ${typ}$(${k}$), intent(in) :: y(:)

        ${typ}$(${k}$), allocatable :: g_j(:),q_i(:),q_i_m(:),q_i_p(:)
        integer :: j,i
        logical :: found_x_j
        ${typ}$(${k}$), parameter :: zero = 0
        #: if d == 'cp'
        logical :: num_zero, den_zero
        logical, allocatable :: inf(:)
        #:endif

        this%x = x
        this%y = y
        this%N = size(x)

        do i = 1,this%N
            do j = i+1,this%N
                if (is_close(x(i),x(j))) then
                    call interp_distinct_error('MTT')
                end if
            end do
        end do

        allocate(this%x_p(this%N),this%y_p(this%N),g_j(2:this%N),this%a(2:this%N))
        this%x_p = x
        this%y_p = y
        g_j = y(2:)-y(1)
        this%a = 0
        #: if d == 'cp'
        allocate(inf(this%N),source = .false.)
        #: endif

        ! Stage (a)
        this%t = 1
        do j = 2,this%N
            if (.not.is_finite(g_j(j)).or.is_close(g_j(j),zero)) then
                found_x_j = .false.
                do i = j+1,this%N
                    if (is_finite(g_j(i)).and..not.is_close(g_j(i),zero)) then
                        this%x_p([i,j]) = this%x_p([j,i])
                        g_j([i,j]) = g_j([j,i])
                        this%y_p([i,j]) = this%y_p([j,i])
                        #:if d == 'cp'
                        inf([i,j]) = inf([j,i])
                        #:endif
                        found_x_j = .true.
                        exit
                    end if
                end do
                if (.not.found_x_j) then
                    if (all(is_finite(g_j(j:)))) then
                        ! Stage (b)
                        exit
                    else
                        ! Stage (c)
                        call interp_exist_error('MTT, Stage (c)')
                    end if
                end if
            end if
            this%a(j) = g_j(j)/(this%x_p(j)-this%x_p(j-1))
            if (j<this%N) then
                #:if d == 're'
                g_j(j+1:) = this%a(j)*(this%x_p(j+1:)-this%x_p(j-1))/g_j(j+1:) - 1.0_${k}$
                #:else
                do i = j+1, this%N
                    if (inf(i)) then
                        g_j(i) = -1.0_${k}$
                        inf(i) = .false.
                    else
                        den_zero = is_close(g_j(i),zero).and.is_finite(g_j(i))
                        num_zero = is_close(this%a(j)*(this%x_p(i)-this%x_p(j-1)),zero)
                        num_zero = num_zero.and.is_finite(this%a(j)*(this%x_p(i)-this%x_p(j-1)))
                        g_j(i) = this%a(j)*(this%x_p(i)-this%x_p(j-1))/g_j(i) - 1.0_${k}$
                        if (den_zero.and..not.num_zero) then
                            inf(i) = .true.
                        end if
                    end if
                end do
                #:endif
            end if
            this%t = j
        end do

        ! Termination stage
        if (this%t == 1) then
            this%is_constant = .true.
        else
            allocate(q_i(this%N),q_i_m(this%N),q_i_p(this%N))
            q_i_m = 1.0_${k}$
            q_i = q_i_m + this%a(3)*(this%x-this%x_p(2))
            if (any(is_close(q_i,zero))) call interp_exist_error('MTT, q_2')
            do i = 3,this%t-1
                q_i_p = q_i + this%a(i+1)*(this%x-this%x_p(i))*q_i_m
                if (any(is_close(q_i_p,zero))) call interp_exist_error('MTT, q_i')
                q_i_m = q_i
                q_i = q_i_p
            end do
        end if

    end subroutine MTT_init_${d}$_${k}$

    function MTT_eval_scalar_${d}$_${k}$(this,x) result(res)
        class(MTT_interp_${d}$(${k}$)), intent(in) :: this
        ${typ}$(${k}$), intent(in) :: x
        ${typ}$(${k}$) :: res

        integer :: i
        ${typ}$(${k}$) :: frac

        res = this%y(1)

        if (this%t > 1) then

            frac = 1.0_${k}$

            do i = this%t,3,-1
                frac = 1.0_${k}$ + this%a(i)*(x - this%x_p(i-1))/frac
            end do

            frac = this%a(2)*(x-this%x_p(1))/frac
            res = res + frac
        end if

    end function MTT_eval_scalar_${d}$_${k}$

    function MTT_eval_vec_${d}$_${k}$(this,x) result(res)
        class(MTT_interp_${d}$(${k}$)), intent(in) :: this
        ${typ}$(${k}$), intent(in) :: x(:)
        ${typ}$(${k}$), allocatable :: res(:)

        integer :: i

        allocate(res(size(x)))
        res = 0

        do i = 1,size(x)
            res(i) = this%eval(x(i))
        end do

    end function MTT_eval_vec_${d}$_${k}$

    #:endfor
    #:endfor

    #:for k in KINDS
    elemental function is_finite_re_${k}$(x) result(res)
        real(${k}$), intent(in) :: x
        logical :: res

        res = ieee_is_finite(x)
    end function is_finite_re_${k}$

    elemental function is_finite_cp_${k}$(z) result(res)
        complex(${k}$), intent(in) :: z
        logical :: res

        res = ieee_is_finite(real(z,kind=${k}$)).and.ieee_is_finite(aimag(z))
    end function is_finite_cp_${k}$

    #:endfor

    subroutine interp_exist_error(error_message)
        character(len=*), intent(in) :: error_message

        write(stderr,'(a,a)') 'Error! Interpolant does not exist: ', error_message
        error stop
    end subroutine interp_exist_error

    subroutine interp_distinct_error(error_message)
        character(len=*), intent(in) :: error_message

        write(stderr,'(a,a)') 'Error! Interpolation points are not distinct in: ', error_message
        error stop
    end subroutine interp_distinct_error
end module rational_interpolation